" Environment
" ===========

" Redirect VIM files
" ------------------

" $MYVIMRC will also be redirected, but this is done by setting the $VIMINIT
" environment variable before starting VIM, e.g., by exporting $VIMINIT from
" .zshrc. See https://tlvince.com/vim-respect-xdg.

" ### Swap Files
set directory=$XDG_CACHE_HOME/vim,/tmp

" ### Backup Directory
set backupdir=$XDG_CACHE_HOME/vim,/tmp

" ### Viminfo File
" h: viminfo-file-name
set viminfo+=n$XDG_CACHE_HOME/vim/viminfo

" ### Runtime Path
set runtimepath=$XDG_CONFIG_HOME/vim,$XDG_CONFIG_HOME/vim/after,$VIM,$VIMRUNTIME

" Appearance
" ==========

" Colors
" ------

" ### SetColorGroup
function! s:SetColorGroup(group, fg, ...)
  let hi_cmd = 'highlight ' . a:group

  if strlen(a:fg)
    let hi_cmd .= ' ctermfg=' . a:fg
  endif
  if a:0 >= 1 && strlen(a:1)
    let hi_cmd .= ' ctermbg=' . a:1
  endif
  if a:0 >= 2 && strlen(a:2)
    let hi_cmd .= ' cterm=' . a:2
  endif

  execute hi_cmd
endfunction

" ### Palette
set background=dark

let s:bwc_plain = 15
let s:bwc_snow = 15
let s:bwc_coal = 16
let s:bwc_brightgravel = 252
let s:bwc_lightgravel = 245
let s:bwc_gravel = 243
let s:bwc_mediumgravel = 241
let s:bwc_deepgravel = 238
let s:bwc_deepergravel = 236
let s:bwc_darkgravel = 235
let s:bwc_blackgravel = 233
" let s:bwc_blackestgravel = 232
let s:bwc_dalespale = 221
let s:bwc_dirtyblonde = 222
let s:bwc_taffy = 196
" let s:bwc_saltwatertaffy = 121
let s:bwc_tardis = 39
let s:bwc_orange = 214
let s:bwc_lime = 154
let s:bwc_dress = 211
let s:bwc_toffee = 137
" let s:bwc_coffee = 173
" let s:bwc_darkroast = 95
let s:bwc_gutter = s:bwc_blackgravel
let s:bwc_tabline = s:bwc_blackgravel

" ### General/UI
call s:SetColorGroup('Normal', s:bwc_plain, s:bwc_blackgravel)
call s:SetColorGroup('Folded', s:bwc_mediumgravel, 'bg', 'none')
call s:SetColorGroup('VertSplit', s:bwc_lightgravel, 'bg', 'none')
call s:SetColorGroup('CursorLine', '', s:bwc_darkgravel, 'none')
call s:SetColorGroup('CursorColumn', '', s:bwc_darkgravel)
call s:SetColorGroup('ColorColumn', '', s:bwc_darkgravel)
call s:SetColorGroup('TabLine', s:bwc_plain, s:bwc_tabline, 'none')
call s:SetColorGroup('TabLineFill', s:bwc_plain, s:bwc_tabline, 'none')
call s:SetColorGroup('TabLineSel', s:bwc_coal, s:bwc_tardis, 'none')
call s:SetColorGroup('MatchParen', s:bwc_dalespale, s:bwc_darkgravel, 'bold')
call s:SetColorGroup('NonText', s:bwc_deepgravel, 'bg')
call s:SetColorGroup('SpecialKey', s:bwc_deepgravel, 'bg')
call s:SetColorGroup('Visual', '', s:bwc_deepgravel)
call s:SetColorGroup('VisualNOS', '', s:bwc_deepgravel)
call s:SetColorGroup('Search', s:bwc_coal, s:bwc_dalespale, 'bold')
call s:SetColorGroup('IncSearch', s:bwc_coal, s:bwc_tardis, 'bold')
call s:SetColorGroup('Underlined', 'fg', '', 'underline')
call s:SetColorGroup('StatusLine', s:bwc_coal, s:bwc_tardis, 'bold')
call s:SetColorGroup('StatusLineNC', s:bwc_snow, s:bwc_deepgravel, 'bold')
call s:SetColorGroup('Directory', s:bwc_dirtyblonde, '', 'bold')
call s:SetColorGroup('Title', s:bwc_lime)
call s:SetColorGroup('ErrorMsg', s:bwc_taffy, 'bg', 'bold')
call s:SetColorGroup('MoreMsg', s:bwc_dalespale, '', 'bold')
call s:SetColorGroup('ModeMsg', s:bwc_dirtyblonde, '', 'bold')
call s:SetColorGroup('Question', s:bwc_dirtyblonde, '', 'bold')
call s:SetColorGroup('WarningMsg', s:bwc_dress, '', 'bold')
call s:SetColorGroup('Tag', '', '', 'bold')

" ### Gutter
call s:SetColorGroup('LineNr', s:bwc_mediumgravel, s:bwc_gutter)
call s:SetColorGroup('SignColumn', '', s:bwc_gutter)
call s:SetColorGroup('FoldColumn', s:bwc_mediumgravel, s:bwc_gutter)

" ### Cursor
call s:SetColorGroup('Cursor', s:bwc_coal, s:bwc_tardis, 'bold')
call s:SetColorGroup('vCursor', s:bwc_coal, s:bwc_tardis, 'bold')
call s:SetColorGroup('iCursor', s:bwc_coal, s:bwc_tardis, 'none')

" ### Syntax highlighting
call s:SetColorGroup('Special', s:bwc_plain)
call s:SetColorGroup('Comment', s:bwc_gravel)
call s:SetColorGroup('Todo', s:bwc_snow, 'bg', 'bold')
call s:SetColorGroup('SpecialComment', s:bwc_snow, 'bg', 'bold')
call s:SetColorGroup('String', s:bwc_dirtyblonde)
call s:SetColorGroup('Statement', s:bwc_taffy, '', 'bold')
call s:SetColorGroup('Keyword', s:bwc_taffy, '', 'bold')
call s:SetColorGroup('Conditional', s:bwc_taffy, '', 'bold')
call s:SetColorGroup('Operator', s:bwc_taffy, '', 'none')
call s:SetColorGroup('Label', s:bwc_taffy, '', 'none')
call s:SetColorGroup('Repeat', s:bwc_taffy, '', 'none')
call s:SetColorGroup('Identifier', s:bwc_orange, '', 'none')
call s:SetColorGroup('Function', s:bwc_orange, '', 'none')
call s:SetColorGroup('PreProc', s:bwc_lime, '', 'none')
call s:SetColorGroup('Macro', s:bwc_lime, '', 'none')
call s:SetColorGroup('Define', s:bwc_lime, '', 'none')
call s:SetColorGroup('PreCondit', s:bwc_lime, '', 'bold')
call s:SetColorGroup('Constant', s:bwc_toffee, '', 'bold')
call s:SetColorGroup('Character', s:bwc_toffee, '', 'bold')
call s:SetColorGroup('Boolean', s:bwc_toffee, '', 'bold')
call s:SetColorGroup('Number', s:bwc_toffee, '', 'bold')
call s:SetColorGroup('Float', s:bwc_toffee, '', 'bold')
call s:SetColorGroup('SpecialChar', s:bwc_dress, '', 'bold')
call s:SetColorGroup('Type', s:bwc_dress, '', 'none')
call s:SetColorGroup('StorageClass', s:bwc_taffy, '', 'none')
call s:SetColorGroup('Structure', s:bwc_taffy, '', 'none')
call s:SetColorGroup('Typedef', s:bwc_taffy, '', 'bold')
call s:SetColorGroup('Exception', s:bwc_lime, '', 'bold')
call s:SetColorGroup('Error', s:bwc_snow, s:bwc_taffy, 'bold')
call s:SetColorGroup('Debug', s:bwc_snow, '', 'bold')
call s:SetColorGroup('Ignore', s:bwc_gravel, '', '')

" ### Completion Menu
call s:SetColorGroup('Pmenu', s:bwc_plain, s:bwc_deepergravel)
call s:SetColorGroup('PmenuSel', s:bwc_coal, s:bwc_tardis, 'bold')
call s:SetColorGroup('PmenuSbar', '', s:bwc_deepergravel)
call s:SetColorGroup('PmenuThumb', s:bwc_brightgravel)

" ### Diffs
call s:SetColorGroup('DiffDelete', s:bwc_coal, s:bwc_coal)
call s:SetColorGroup('DiffAdd', '', s:bwc_deepergravel)
call s:SetColorGroup('DiffChange', '', s:bwc_darkgravel)
call s:SetColorGroup('DiffText', s:bwc_snow, s:bwc_deepergravel, 'bold')

" ### Spelling
call s:SetColorGroup('SpellCap', s:bwc_dalespale, 'bg', 'undercurl,bold')
call s:SetColorGroup('SpellBad', '', 'bg', 'undercurl')
call s:SetColorGroup('SpellLocal', '', '', 'undercurl')
call s:SetColorGroup('SpellRare', '', '', 'undercurl')

" Visual Elements
" ---------------

" ### Cursors

if &term =~ 'xterm'
  let s:cursor_normal = "\<Esc>[2 q\<Esc>]12;DeepSkyBlue1\x7"
  let s:cursor_insert = "\<Esc>[5 q\<Esc>]12;DeepSkyBlue1\x7"
  let s:cursor_replace = "\<Esc>[3 q\<Esc>]12;DeepSkyBlue1\x7"
  let s:cursor_reset = s:cursor_insert

  autocmd VimEnter * silent execute '!printf "' . s:cursor_normal . '"'
  autocmd VimLeave * silent execute '!printf "' . s:cursor_reset . '"'

  if exists('$TMUX')
    let s:tmux_pre = "\<Esc>Ptmux;\<Esc>"
    let s:tmux_post = "\<Esc>\\"
    let &t_EI .= s:tmux_pre . s:cursor_normal . s:tmux_post
    let &t_SI .= s:tmux_pre . s:cursor_insert . s:tmux_post
    let &t_SR .= s:tmux_pre . s:cursor_replace . s:tmux_post
  else
    let &t_EI .= s:cursor_normal
    let &t_SI .= s:cursor_insert
    let &t_SR .= s:cursor_replace
  endif
endif

" ### Syntax highlighting
syntax on

" ### Line numbers
set number relativenumber

" ### Cursor line and column
set cursorline colorcolumn=81

" Behavior
" ========

" Indentation
" -----------

" See https://vim.fandom.com/wiki/Indenting_source_code

" ### Default

" Use 2 spaces
set expandtab
set shiftwidth=2
set softtabstop=2

" Auto indent
set autoindent

" ### markdown
augroup indent_markdown
  autocmd!
  autocmd Filetype markdown set shiftwidth=4
  autocmd Filetype markdown set softtabstop=4
augroup END

" Folds
" -----

" ### Functions

" #### GetFoldLevelByHeading
let s:last_fold_level = 0
let s:last_last_fold_level = 0
let s:is_code_block = 0

function! GetFoldLevelByHeading(lnum, ...)
  let l:has_code_blocks = a:0 >= 2 ? a:2 : 0
  let l:line = getline(a:lnum)

  if l:has_code_blocks && l:line[0 : 2] ==# '```'
    let s:is_code_block = !s:is_code_block
  endif

  if !s:is_code_block
    let l:prefix = a:0 >= 1 ? a:1 : ''
    let l:prefix_len = len(l:prefix)
    let l:line_p1 = getline(a:lnum + 1)

    if l:prefix_len ==# 0 || l:line_p1[0 : l:prefix_len - 1] ==# l:prefix

      if l:line_p1[l:prefix_len : ] =~ '^=\+$'
        let s:last_fold_level = 1
        return '>1'
      endif

      if l:line_p1[l:prefix_len : ] =~ '^-\+$'
        let s:last_fold_level = 2
        return '>2'
      endif
    endif

    if l:line[0 : l:prefix_len + 1] ==# l:prefix . '# '
      let s:last_last_fold_level = s:last_fold_level
      let s:last_fold_level = 1
      return '>1'
    endif

    if l:line[0 : l:prefix_len + 2] ==# l:prefix . '## '
      let s:last_last_fold_level = s:last_fold_level
      let s:last_fold_level = 2
      return '>2'
    endif

    if l:line[0 : l:prefix_len + 3] ==# l:prefix . '### '
      let s:last_last_fold_level = s:last_fold_level
      let s:last_fold_level = 3
      return '>3'
    endif

    if l:line[0 : l:prefix_len + 4] ==# l:prefix . '#### '
      let s:last_last_fold_level = s:last_fold_level
      let s:last_fold_level = 4
      return '>4'
    endif

    if l:line[0 : l:prefix_len + 5] ==# l:prefix . '##### '
      let s:last_last_fold_level = s:last_fold_level
      let s:last_fold_level = 5
      return '>5'
    endif

    if l:line[0 : l:prefix_len + 6] ==# l:prefix . '###### '
      let s:last_last_fold_level = s:last_fold_level
      let s:last_fold_level = 6
      return '>6'
    endif

    if l:line[0 : l:prefix_len] ==# l:prefix . '('
      if s:last_fold_level < 7
        let s:last_last_fold_level = s:last_fold_level
      endif
      let s:last_fold_level = 7
      return '>7'
    endif

    if l:line[0 : l:prefix_len] ==# l:prefix . ')'
      let s:last_fold_level = s:last_last_fold_level
      return '<7'
    endif
  endif

  return s:last_fold_level
endfunction

" ### File types

" #### dockerfile
augroup fold_dockerfile
  autocmd!
  autocmd Filetype dockerfile set foldmethod=expr
  autocmd Filetype dockerfile set foldexpr=GetFoldLevelByHeading(v:lnum,'\#\ ')
  autocmd Filetype dockerfile set foldlevel=1
augroup END

" #### markdown
augroup fold_markdown
  autocmd!
  autocmd Filetype markdown set foldmethod=expr
  autocmd Filetype markdown set foldexpr=GetFoldLevelByHeading(v:lnum,'',1)
  autocmd Filetype markdown set foldlevel=2
augroup END

" #### sh
augroup fold_sh
  autocmd!
  autocmd Filetype sh set foldmethod=expr
  autocmd Filetype sh set foldexpr=GetFoldLevelByHeading(v:lnum,'\#\ ')
  autocmd Filetype sh set foldlevel=1
augroup END

" #### vim
augroup fold_vim
  autocmd!
  autocmd Filetype vim set foldmethod=expr
  autocmd Filetype vim set foldexpr=GetFoldLevelByHeading(v:lnum,'\"\ ')
  autocmd Filetype vim set foldlevel=1
augroup END

" #### zsh
augroup fold_zsh
  autocmd!
  autocmd Filetype zsh set foldmethod=expr
  autocmd Filetype zsh set foldexpr=GetFoldLevelByHeading(v:lnum,'\#\ ')
  autocmd Filetype zsh set foldlevel=1
augroup END

" Mappings
" --------

" ### Leader
let mapleader = ' '

" ### Enter normal mode
inoremap kj <Esc>
inoremap jk <Esc>

" ### Navigate splits
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-h> <C-w>h
nnoremap <C-l> <C-w>l

" ### Edit .vimrc
nnoremap <silent> <Leader>ev :vsplit $MYVIMRC<CR>

" ### Save and source current file
nnoremap <silent> <Leader>ss :w<CR>
  \ :source %<CR>
  \ :echo 'Sourced ' . bufname('%')<CR>

" ### Trim trailing whitespace
nnoremap <Leader>tw :%s/\s\+$//gc<CR>

